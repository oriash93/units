apply plugin: 'cpp'
apply plugin: 'google-test'
apply plugin: 'eclipse'

model { 

    repositories {
        libs(PrebuiltLibraries) {
            googleTest {
                headers.srcDir "googleTest/include"
                binaries.withType(StaticLibraryBinary) {
                    staticLibraryFile = file("googleTest/lib/libgtest.a")
                }
            }
        }
    }
    
    components {
        units(NativeLibrarySpec) {
			sources.cpp.source.exclude '**/*.swp'
		}
    }

    binaries {
        withType(SharedLibraryBinarySpec) {
            if (toolChain in VisualCpp) {
                cppCompiler.define "DLL_EXPORT" 
            }
        }

		withType(GoogleTestTestSuiteBinarySpec) {
		    lib library: "googleTest", linkage: "static"
			
			if (toolChain in Gcc) {
	                linker.args "-pthread"
	        }
	    }
    }

    toolChains {
        gcc(Gcc) {
            eachPlatform {
                cppCompiler.withArguments { args ->
                    args << "-std=c++1y"
                }
            }
        }
    }

	testSuites {
        unitsTest {
            sources {
                cpp.source.exclude "**/*.swp"
            }
        }
    }
}

model {
    components {
        main(NativeExecutableSpec) {
            sources {
                cpp {
                    lib library: "units", linkage: "static"
                }
            }
        }
    }
}